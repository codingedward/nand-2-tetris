// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Incremented output...
    Inc16(in=feedback, out=incOut);

    // Modify load to to have the right operator
    // precedence that is reset > load > inc
    Not(in=load, out=notLoad);
    And(a=inc, b=notLoad, out=incAndnotLoad);

    // Inc output?
    Mux16(a=in, b=incOut, sel=incAndnotLoad, out=muxInc);

    // Reset output?
    Mux16(a=muxInc, b=false, sel=reset, out=finalOut);
    
    // Should update?
    Or(a=load, b=inc, out=loadOrInc);
    Or(a=loadOrInc, b=reset, out=update);

    // Now update
    Register(in=finalOut, load=update, out=out, out=feedback);
}
